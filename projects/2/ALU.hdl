// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=notx);
    /**
     * (nx, zx)
     * (0, 0) = xが出力される(前処理なし)
     * (0, 1) = false(0000000000000000=0)が出力される
     * (1, 0) = notxが出力される(反転)
     * (1, 1) = true(1111111111111111=-1)が出力される
     */
    Mux4Way16(a=x, b=false, c=notx, d=true, sel[0]=zx, sel[1]=nx, out=outx);

    Not16(in=y, out=noty);
    /**
     * (ny, zy)
     * (0, 0) = yが出力される(前処理なし)
     * (0, 1) = false(0000000000000000=0)が出力される
     * (1, 0) = notyが出力される(反転)
     * (1, 1) = true(1111111111111111=-1)が出力される
     */
    Mux4Way16(a=y, b=false, c=noty, d=true, sel[0]=zy, sel[1]=ny, out=outy);

    And16(a=outx, b=outy, out=outand);
    Add16(a=outx, b=outy, out=outadd);

    /**
     * f=1: x+y
     * f=0: x&y
     */
    Mux16(a=outand, b=outadd, sel=f, out=outf);

    // no
    Not16(in=outf, out=outnotf);
    Mux16(a=outf, b=outnotf, sel=no, out=finalout);

    // 最終出力とフラグ計算
    And16(a=finalout, b=finalout, out=out, out[15]=ng, out[0..7]=outlow, out[8..15]=outhigh);

    // zr
    Or8Way(in=outlow, out=or1);
    Or8Way(in=outhigh, out=or2);
    Or(a=or1, b=or2, out=or12);
    Not(in=or12, out=zr);
}